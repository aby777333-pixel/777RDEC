/**
 * Fragment shader for the crm hero band — "Dark City Ambience" by Matthias Hurrle
 * (@atzedent), https://codepen.io/atzedent/pen/zxKmgpj
 *
 * Verbatim from the pen. Every `time`, `resolution`, `move` and `wheel` it
 * reads is supplied by <ShaderBackdrop>, which is the same set the pen's own
 * playground supplied. Do not reformat: the whitespace is the author's and a
 * stray edit inside a `#define` changes the image.
 */
export const CRM_FRAGMENT_SRC = `#version 300 es
/*********
* made by Matthias Hurrle (@atzedent)
*/
precision highp float;
out vec4 O;
uniform float time;
uniform vec2 resolution;
uniform vec2 move;
uniform vec2 wheel;
#define FC gl_FragCoord.xy
#define R resolution
#define T (25.+time)
#define S smoothstep
#define N normalize
#define MN min(R.x,R.y)
#define rnd(p) fract(sin(dot(p,vec2(12.9898,78.233)))*345678.)
#define rot(a) mat2(cos((a)-vec4(0,11,33,0)))
float box(vec3 p, vec3 s, float r) {
	p=abs(p)-s+r;
	return length(max(p,.0))+min(.0,max(max(p.x,p.y),p.z))-r;
}
float map(vec3 p) {
	vec3 q=cos(p*1.8+5e2);
	float s=sign(p.y);
	p.y=abs(p.y)-2.5;
	vec2 id=floor(p.xz-s);
	if (mod(id.y,2.)==.0) {
		p.x-=T*.5;
		id.x=floor(p.x-s);
	} 
	float f=1.-dot(abs(fract(p*42.)-.5)-.25,vec3(1))*.5;
	p.xz=fract(p.xz-s)-.5;
	return box(p,vec3(.1+.3*rnd(id),2.-.6*rnd(id),.2),f*f*.0125)-1e-3*f;
}
vec3 norm(vec3 p) {
	float h=1e-3; vec2 k=vec2(-1,1);
	return N(
		k.xyy*map(p+k.xyy*h)+
		k.yxy*map(p+k.yxy*h)+
		k.yyx*map(p+k.yyx*h)+
		k.xxx*map(p+k.xxx*h)
	);
}
bool march(inout vec3 p, vec3 rd, out float dd) {
	for (int i; i++<400;) {
		float d=map(p);
		if (abs(d)<1e-3) return true;
		if (dd>15.) return false;
		p+=rd*d*.5;
		dd+=d*.5;
	}
}
float occ(vec3 p, vec3 n, float d) {
	return clamp(map(p+n*d)/d,.0,1.);
}
vec3 dir(vec2 uv, vec3 p, vec3 t, float z) {
	vec3 up=vec3(0,1,0),
	f=N(t-p),
	r=N(cross(up,f)),
	u=N(cross(f,r));
	return mat3(r,u,f)*N(vec3(uv,z));
}
void cam(inout vec3 p) {
	p.xz*=rot(.2-move.x/MN+.2*T*.01);
}
vec3 render(vec2 uv) {
	vec3 col=vec3(0),
	p=vec3(0,-.3,-23.5-wheel.y/MN-1e2*sin(T*5e-3));
	cam(p);
	vec3 rd=dir(uv,p,vec3(0,5.5,0),1.2), lp=p;
	lp.z+=.5;
	float dd;
	if (march(p,rd,dd)) {
		vec3 n=norm(p), l=N(lp-p);
		float dif=clamp(dot(l,n),.0,1.),
		spe=pow(clamp(dot(N(lp-rd),n),.0,1.),21.),
		ao=occ(p,n,.5)*.8*occ(p,n,1.),
		ld=distance(lp,p), atten=1./(1.+ld*.25+ld*ld*.125);
		vec3 mat=vec3(4,1.6,.6);
		col+=.08+dif*mat*ao*atten;
		col+=spe*atten;
	}
	col=mix(vec3(0),col,exp(-125e-5*dd*dd*dd));
	col=tanh(col*col);
	col=sqrt(col);
	col=mix(vec3(0),col,min(time*.3,1.));
	// vignette
	vec2 c=FC/R;
	c*=1.-c.yx;
	float vig=c.x*c.y*25.;
	vig=pow(vig,.5);
	col*=vig;
	return col;
}
void main() {
	vec2 uv=(FC-.5*R)/MN;
	vec3 col=render(uv);
  O=vec4(col,1);
}`
